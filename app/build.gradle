apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

def keyPropFile = rootProject.file("keystore.properties")
def keyProp = new Properties()
keyProp.load(new FileInputStream(keyPropFile))
def fabricPropFile = project.file("fabric.properties")
def fabricProp = new Properties()
fabricProp.load(new FileInputStream(fabricPropFile))

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.epishie.news"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "0.0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField 'String', 'NEWS_API_KEY', "\"${keyProp.getProperty("NewsApiKey")}\""
        buildConfigField 'String', 'POST_LIGHT_API_KEY', "\"${keyProp.getProperty("PostLightApiKey")}\""

        manifestPlaceholders = [ api_key_fabric: "${fabricProp.getProperty("apiKey")}"]
    }
    signingConfigs {
        release {
            storeFile rootProject.file("release.jks")
            storePassword keyProp["StorePassword"]
            keyAlias keyProp["KeyAlias"]
            keyPassword keyProp["KeyPassword"]
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }
}

ext {
    supportLibVersion = '26.0.0'
    archLibVersion = '1.0.0-alpha5'
    daggerVersion = '2.11'
    retrofitVersion = '2.3.0'
    rxBindingVersion = '2.0.0'

    betaDistributionReleaseNotesFilePath = 'beta_notes.txt'
    betaDistributionEmailsFilePath = 'beta_users.txt'
    betaDistributionNotifications = true
}

dependencies {
    implementation "com.android.support:appcompat-v7:$supportLibVersion",
            "com.android.support:design:$supportLibVersion",
            "com.android.support:percent:$supportLibVersion",
            "com.android.support:customtabs:$supportLibVersion",
            "com.android.support.constraint:constraint-layout:1.0.2",
            "android.arch.lifecycle:runtime:$archLibVersion",
            "android.arch.lifecycle:extensions:$archLibVersion",
            "android.arch.persistence.room:runtime:$archLibVersion",
            "android.arch.persistence.room:rxjava2:$archLibVersion",
            "com.google.dagger:dagger:$daggerVersion",
            "io.reactivex.rxjava2:rxjava:2.1.2",
            "io.reactivex.rxjava2:rxandroid:2.0.1",
            "com.jakewharton.rxrelay2:rxrelay:2.0.0",
            "com.trello.rxlifecycle2:rxlifecycle-android-lifecycle-kotlin:2.1.0",
            "com.squareup.retrofit2:retrofit:$retrofitVersion",
            "com.squareup.retrofit2:converter-gson:$retrofitVersion",
            "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion",
            "com.squareup.picasso:picasso:2.5.2",
            "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0",
            "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBindingVersion",
            "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxBindingVersion",
            "com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC3",
            "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation ('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
    compileOnly "javax.annotation:jsr250-api:1.0"
    debugImplementation "com.facebook.stetho:stetho:1.5.0",
            "com.facebook.stetho:stetho-okhttp3:1.5.0"
    kapt "android.arch.lifecycle:compiler:$archLibVersion",
            "com.google.dagger:dagger-compiler:$daggerVersion",
            "android.arch.persistence.room:compiler:$archLibVersion"
    testImplementation 'junit:junit:4.12',
            "org.assertj:assertj-core:3.8.0",
            "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
